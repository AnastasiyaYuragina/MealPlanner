plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'kotlinx-serialization'
    id 'com.google.firebase.crashlytics'
}

//This is a major version of the app.
//Change this value every time when you create release and next release version is greater
def majorVersion = 1

//This is a minor version of the app
//Change this value every time when you create release (usually at the end of sprint)
def minorVersion = 2

//Hotfix version. Change this value every time when you create hotfix from master branch
def hotfixVersion = 0

//Build number. This value is computed according to current jenkins system BUILD_NUMBER
def buildNumber = Integer.valueOf(System.env.BUILD_NUMBER ?: 0)

//According to doc, max number is 2100000000
//Stop using auto numeration after this value is reached
def maxSafeVersionCode = 1000000000

def calculatedVersionNumber = majorVersion * 10000000 + minorVersion * 100000 + hotfixVersion * 10000 + buildNumber

def defaultVersionName = "$majorVersion.$minorVersion.$hotfixVersion.$buildNumber"

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.anastasiyayuragina.mealplanner"
        minSdk 24
        targetSdk 32
        //Don"t use number greater than maxSafeVersionCode
        versionCode = calculatedVersionNumber < maxSafeVersionCode ? calculatedVersionNumber : 0
        versionName = defaultVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.anastasiyayuragina.mealplanner'
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.1"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.gms:play-services-auth:20.2.0'

    //    compose
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1"
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    //    navigation
    implementation "androidx.navigation:navigation-compose:2.5.0-rc02"

    //    paging
    implementation 'androidx.paging:paging-compose:1.0.0-alpha15'

    //    hilt
    implementation "com.google.dagger:hilt-android:2.40.5"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha03'
    kapt "com.google.dagger:hilt-compiler:2.40.5"

    //    accompanist
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"

    //    preferences
    implementation "androidx.preference:preference-ktx:1.2.0"

    //    serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:1.2.0"

    //    logcat
    implementation 'com.squareup.logcat:logcat:0.1'

    //    coil
    implementation "com.github.skydoves:landscapist-coil:1.4.3"

    //    firebase
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'

    //    test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}